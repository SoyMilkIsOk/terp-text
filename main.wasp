app TerpText {
  wasp: {
    version: "^0.11.4"
  },
  title: "TerpText",
  dependencies: [
    ("react-icons", "5.0.1"),
    ("@chakra-ui/react", "2.8.0"),
    ("react-modal", "3.16.1"),
    ("js-cookie", "3.0.5"),
    ("twilio","4.21.0")
  ],
  client: {
    rootComponent: import { App } from "@client/App.jsx"
  },
  db: {
    system: PostgreSQL,
    seeds: [
      import { devSeed } from "@server/devSeed.js"
    ],
  },
  auth: {
    userEntity: User,
    methods: {
      email: {
        fromField: {
          name: "TerpText",
          email: "terps@terpmetrix.com"
        },
        emailVerification: {
          clientRoute: EmailVerificationRoute,
        },
        passwordReset: {
          clientRoute: PasswordResetRoute,
        },
        allowUnverifiedLogin: false,
      },
    },
    onAuthFailedRedirectTo: "/login",
    onAuthSucceededRedirectTo: "/"
  },
  emailSender: {
    provider: SendGrid,
  }
}

/////// AUTH ///////

route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import { Login } from "@client/pages/auth.jsx"
}

route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import { Signup } from "@client/pages/auth.jsx"
}

route RequestPasswordResetRoute { path: "/request-password-reset", to: RequestPasswordResetPage }
page RequestPasswordResetPage {
  component: import { RequestPasswordReset } from "@client/pages/auth.jsx",
}

route PasswordResetRoute { path: "/password-reset", to: PasswordResetPage }
page PasswordResetPage {
  component: import { PasswordReset } from "@client/pages/auth.jsx",
}

route EmailVerificationRoute { path: "/email-verification", to: EmailVerificationPage }
page EmailVerificationPage {
  component: import { EmailVerification } from "@client/pages/auth.jsx",
}


/////// ENTITIES ///////

entity User {=psl
    id           Int          @id @default(autoincrement())
    email        String?       @unique
    phone        String?      @unique
    password     String?
    isEmailVerified Boolean @default(false)
    emailVerificationSentAt DateTime?
    passwordResetSentAt DateTime?
    strains      UserStrain[]
    notificationType String?
psl=}

entity Dispensary {=psl
    id      Int      @id @default(autoincrement())
    slug    String   @unique
    name    String   @unique
    phone   String?   @unique
    website String?   @unique
    adminEmail String?
    strains DispensaryStrain[]
    userStrains UserStrain[]
psl=}

entity Strain {=psl
    id           Int          @unique @id @default(autoincrement())
    name         String @unique
    users        UserStrain[]
    dispensaries DispensaryStrain[]
    grower       String @default("grower")
psl=}

entity UserStrain {=psl
    id       Int    @default(autoincrement())
    user     User   @relation(fields: [userId], references: [id], onDelete: Cascade)
    userId   Int
    strain   Strain @relation(fields: [strainId], references: [id], onDelete: Cascade)
    strainId Int
    dispensary Dispensary @relation(fields: [dispensarySlug], references: [slug], onDelete: Cascade)
    dispensarySlug String @default("dispensary slug")
    // unique compound index on userId, strainId, dispensaryName
    @@id([userId, strainId, dispensarySlug])
psl=}

entity DispensaryStrain {=psl
    id             Int          @id @default(autoincrement())
    dispensary     Dispensary   @relation(fields: [dispensarySlug], references: [slug], onDelete: Cascade)
    dispensarySlug String
    strain         Strain       @relation(fields: [strainName], references: [name], onDelete: Cascade)
    strainName     String @default("strain name")
    available      Boolean      @default(false)
    availableDate  DateTime     @default(now())
psl=}

/////// ACTIONS ///////

action createDispensary {
  fn: import { createDispensary } from "@server/actions.js",
  entities: [Dispensary]
}

action createStrain {
  fn: import { createStrain } from "@server/actions.js",
  entities: [Strain, DispensaryStrain, Dispensary]
}

action deleteStrain {
  fn: import { deleteStrain } from "@server/actions.js",
  entities: [Strain, DispensaryStrain, Dispensary]
}

action linkStrain {
  fn: import { linkStrain } from "@server/actions.js",
  entities: [DispensaryStrain]
}

action updateStrainAvailability {
  fn: import { updateStrainAvailability } from "@server/actions.js",
  entities: [Dispensary, DispensaryStrain]
}

action deleteUserStrain {
  fn: import { deleteUserStrain } from "@server/actions.js",
  entities: [UserStrain]
}

action createUserStrain {
  fn: import { createUserStrain } from "@server/actions.js",
  entities: [UserStrain]
}

action deleteUserDispensary {
  fn: import { deleteUserDispensary } from "@server/actions.js",
  entities: [UserStrain]
}

action createUserDispensary {
  fn: import { createUserDispensary } from "@server/actions.js",
  entities: [UserStrain]
}

action unsubscribeFromTexts {
  fn: import { unsubscribeFromTexts } from "@server/actions.js",
  entities: [UserStrain]
}

action updatePhone {
  fn: import { updatePhone } from "@server/actions.js",
  entities: [User]
}

action createDispensaryStrain {
  fn: import { createDispensaryStrain } from "@server/actions.js",
  entities: [DispensaryStrain]
}

action sendStrainNotification {
  fn: import { sendStrainNotification } from "@server/actions.js",
  entities: [UserStrain, Dispensary, Strain]
}

action updateUserNotificationType {
  fn: import { updateUserNotificationType } from "@server/actions.js",
  entities: [User]
}

/////// QUERIES ///////

query getDispensary {
  fn: import { getDispensary } from "@server/queries.js",
  entities: [Dispensary]
}

query getStrains {
  fn: import { getStrains } from "@server/queries.js",
  entities: [Strain, Dispensary, DispensaryStrain]
}

query getUser {
  fn: import { getUser } from "@server/queries.js",
  entities: [User, UserStrain]
}

query getAllDispensaries {
  fn: import { getAllDispensaries } from "@server/queries.js",
  entities: [Dispensary]
}

query getAllStrains {
  fn: import { getAllStrains } from "@server/queries.js",
  entities: [Strain]
}

/////// ROUTES ///////

route HomeRoute { path: "/", to: HomePage }

page HomePage {
  component: import { Home } from "@client/pages/Home.jsx",
  authRequired: false
}

route DispensaryRoute { path: "/:slug", to: DispensaryPage }

page DispensaryPage {
  component: import { DispensaryPage } from "@client/pages/Dispensary.jsx",
  authRequired: false
}

route DispensaryDashboardRoute { path: "/:slug/dashboard", to: DispensaryDashboardPage }  

page DispensaryDashboardPage {
  component: import { DispensaryDashboard } from "@client/pages/DispensaryDashboard.jsx",
  authRequired: true
}

route UserProfileRoute { path: "/user/profile", to: UserProfilePage }

page UserProfilePage {
  component: import { UserProfile } from "@client/pages/UserProfile.jsx",
  authRequired: true
}