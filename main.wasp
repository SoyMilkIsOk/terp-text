app TerpText {
  wasp: {
    version: "^0.11.4"
  },
  title: "TerpText",
  dependencies: [
    ("react-icons", "5.0.1"),
    ("@chakra-ui/react", "2.8.0"),
    ("react-modal", "3.16.1"),
    ("js-cookie", "3.0.5")
  ],
  client: {
    rootComponent: import { App } from "@client/App.jsx"
  },
  db: {
    system: PostgreSQL,
    seeds: [
      import { devSeed } from "@server/devSeed.js"
    ],
  },
  auth: {
    userEntity: User,
    methods: {
      usernameAndPassword: {}
    },
    onAuthFailedRedirectTo: "/login",
    onAuthSucceededRedirectTo: "/"
  }
}

/////// AUTH ///////

route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@client/pages/auth/Login.jsx"
}
route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import Signup from "@client/pages/auth/Signup.jsx"
}

/////// ENTITIES ///////

entity User {=psl
    id           Int          @id @default(autoincrement())
    username     String      @unique @default(cuid())
    phone        String?      @unique
    password     String
    strains      UserStrain[]
psl=}

entity Dispensary {=psl
    id      Int      @id @default(autoincrement())
    slug    String   @unique
    name    String   @unique
    phone   String?   @unique
    website String?   @unique
    strains DispensaryStrain[]
    userStrains UserStrain[]
psl=}

entity Strain {=psl
    id           Int          @id @default(autoincrement())
    name         String @unique
    users        UserStrain[]
    dispensaries DispensaryStrain[]
    grower       String @default("grower")
psl=}

entity UserStrain {=psl
    id       Int    @default(autoincrement())
    user     User   @relation(fields: [userId], references: [id])
    userId   Int
    strain   Strain @relation(fields: [strainId], references: [id])
    strainId Int
    dispensary Dispensary @relation(fields: [dispensarySlug], references: [slug])
    dispensarySlug String @default("dispensary slug")
    // unique compound index on userId, strainId, dispensaryName
    @@id([userId, strainId, dispensarySlug])
psl=}

entity DispensaryStrain {=psl
    id             Int          @id @default(autoincrement())
    dispensary     Dispensary   @relation(fields: [dispensarySlug], references: [slug])
    dispensarySlug String
    strain         Strain       @relation(fields: [strainName], references: [name])
    strainName     String @default("strain name")
    available      Boolean      @default(false)
    availableDate  DateTime     @default(now())
psl=}

/////// ACTIONS ///////

action createUser {
  fn: import { createUser } from "@server/actions.js",
  entities: [User]
}

action createDispensary {
  fn: import { createDispensary } from "@server/actions.js",
  entities: [Dispensary]
}

action createStrain {
  fn: import { createStrain } from "@server/actions.js",
  entities: [Strain, DispensaryStrain, Dispensary]
}

action deleteStrain {
  fn: import { deleteStrain } from "@server/actions.js",
  entities: [Strain, DispensaryStrain, Dispensary]
}

action linkStrain {
  fn: import { linkStrain } from "@server/actions.js",
  entities: [DispensaryStrain]
}

action updateStrainAvailability {
  fn: import { updateStrainAvailability } from "@server/actions.js",
  entities: [Dispensary, DispensaryStrain]
}

action deleteUserStrain {
  fn: import { deleteUserStrain } from "@server/actions.js",
  entities: [UserStrain]
}

action createUserStrain {
  fn: import { createUserStrain } from "@server/actions.js",
  entities: [UserStrain]
}

action deleteUserDispensary {
  fn: import { deleteUserDispensary } from "@server/actions.js",
  entities: [UserStrain]
}

action createUserDispensary {
  fn: import { createUserDispensary } from "@server/actions.js",
  entities: [UserStrain]
}

action unsubscribeFromTexts {
  fn: import { unsubscribeFromTexts } from "@server/actions.js",
  entities: [UserStrain]
}

action updatePhone {
  fn: import { updatePhone } from "@server/actions.js",
  entities: [User]
}

action createDispensaryStrain {
  fn: import { createDispensaryStrain } from "@server/actions.js",
  entities: [DispensaryStrain]
}

/////// QUERIES ///////

query getDispensary {
  fn: import { getDispensary } from "@server/queries.js",
  entities: [Dispensary]
}

query getStrains {
  fn: import { getStrains } from "@server/queries.js",
  entities: [Strain, Dispensary, DispensaryStrain]
}

query getUser {
  fn: import { getUser } from "@server/queries.js",
  entities: [User, UserStrain]
}

query getAllDispensaries {
  fn: import { getAllDispensaries } from "@server/queries.js",
  entities: [Dispensary]
}

query getAllStrains {
  fn: import { getAllStrains } from "@server/queries.js",
  entities: [Strain]
}

/////// ROUTES ///////

route HomeRoute { path: "/", to: HomePage }

page HomePage {
  component: import { Home } from "@client/pages/Home.jsx",
  authRequired: false
}

route DispensaryRoute { path: "/:slug", to: DispensaryPage }

page DispensaryPage {
  component: import { DispensaryPage } from "@client/pages/Dispensary.jsx",
  authRequired: false
}

route DispensaryDashboardRoute { path: "/:slug/dashboard", to: DispensaryDashboardPage }  

page DispensaryDashboardPage {
  component: import { DispensaryDashboard } from "@client/pages/DispensaryDashboard.jsx",
  authRequired: true
}

route UserProfileRoute { path: "/:userName/profile", to: UserProfilePage }

page UserProfilePage {
  component: import { UserProfile } from "@client/pages/UserProfile.jsx",
  authRequired: true
}